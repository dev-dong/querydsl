plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'  // 최신 안정 버전
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'study'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot 핵심 의존성
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // H2 Database
    runtimeOnly 'com.h2database:h2'

    // QueryDSL 최신 버전
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // 테스트 관련 의존성
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// QueryDSL Q클래스 생성 위치를 지정
def querydslDir = "src/main/generated"

// QueryDSL Q클래스 소스 디렉토리 설정
sourceSets {
    main.java.srcDirs += [ querydslDir ]
}

// clean 태스크 실행시 생성된 Q클래스 삭제
clean {
    delete file(querydslDir)
}

tasks.named('test') {
    useJUnitPlatform()
}

// Querydsl 설정부
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}
